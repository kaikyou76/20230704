Sub カラム詳細一覧CSV出力()

'*******************************************************************************
'* マクロ概要:各テーブル定義書の詳細情報を、CSV形式出力する
'*
'* 引　　数:なし
'*
'* 戻　　値:なし
'*
'* 作成日　:2011/01/20
'*
'* 作成者　:KSC 遠藤
'*
'* 更新日　:2011/02/02
'*
'*******************************************************************************

'CSVファイル名
Const FILE_NAME = "\カラム詳細一覧.csv"

Dim sh_num%, sel_rw%
Dim FF              As Integer      'FreeFile
Dim select_sheet    As Worksheet    'データ取得先シート

Dim db_name         As String       'データベース名
Dim filegroup       As String       'ファイルグループ
Dim tbl_name        As String       'テーブル論理名
Dim tbl_id          As String       'テーブル物理名
Dim schema          As String       'スキーマ名
Dim no              As String       'カラムNO
Dim clm_name        As String       'カラム論理名
Dim clm_id          As String       'カラム物理名
Dim datatype        As String       'データ型
Dim keta            As String       '桁数
Dim keta_m          As String       '少数桁
Dim nn              As String       'NOTNULL
Dim key             As String       'KEY
Dim strg            As String       'STRG
Dim bikou           As String       '備考


'FreeFile値取得
FF = FreeFile

'ファイルを開く(新規&上書き)
 Open ThisWorkbook.Path & FILE_NAME For Output As #FF
 
 Write #FF, "データベース名", "ファイルグループ", "テーブル論理名", "テーブル物理名", "スキーマ名", "ＮＯ", "カラム論理名", "カラム物理名", "データ型", "桁数", "少数桁", "ＮＵＬＬ", "ＫＥＹ", "ＳＴＲＧ", "備考"

'データ取得元シート毎にループする
For sh_num = 5 To ThisWorkbook.Worksheets.Count

    'データ取得元シート番号をセット
    Set select_sheet = ThisWorkbook.Worksheets(sh_num)
    
    '10～50行目(カラム論理名格納行)までループ
    For sel_rw = 10 To 5000
        
        'カラム論理名取得
        clm_name = select_sheet.Cells(sel_rw, 7)
        
        '以下の条件値を取得した場合は出力しない
        If IsNull(clm_name) Then
            Exit For
        ElseIf clm_name = "作成処理ＩＤ" Then
            Exit For
        ElseIf clm_name = "作成ユーザＩＤ" Then
            Exit For
        ElseIf clm_name = "作成タイムスタンプ" Then
            Exit For
        ElseIf clm_name = "最終更新処理ＩＤ" Then
            Exit For
        ElseIf clm_name = "最終更新ユーザＩＤ" Then
            Exit For
        ElseIf clm_name = "最終更新タイムスタンプ" Then
            Exit For
        ElseIf clm_name = "" Then
            Exit For
        End If
        
        '各データ取得
        db_name = select_sheet.Cells(1, 23)
        filegroup = select_sheet.Cells(2, 23)
        tbl_name = select_sheet.Cells(1, 6)
        tbl_id = select_sheet.Cells(2, 6)
        schema = select_sheet.Cells(1, 29)
        no = select_sheet.Cells(sel_rw, 1)
        clm_id = select_sheet.Cells(sel_rw, 15)
        datatype = select_sheet.Cells(sel_rw, 23)
        keta = select_sheet.Cells(sel_rw, 25)
        keta_m = select_sheet.Cells(sel_rw, 26)
        nn = select_sheet.Cells(sel_rw, 28)
        key = select_sheet.Cells(sel_rw, 2)
        strg = select_sheet.Cells(sel_rw, 27)
        bikou = select_sheet.Cells(sel_rw, 29)
        
        
        'データの書き出し
        Write #FF, db_name, filegroup, tbl_name, tbl_id, schema, no, clm_name, clm_id, datatype, keta, keta_m, nn, key, strg, bikou
     
    Next sel_rw
    
Next sh_num

'ファイルを閉じる
Close #FF

Call MsgBox("処理終了")

End Sub