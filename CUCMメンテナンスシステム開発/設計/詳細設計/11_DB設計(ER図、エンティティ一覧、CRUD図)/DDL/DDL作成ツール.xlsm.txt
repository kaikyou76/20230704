'
' テーブル仕様書Excel文書ファイルからDDL文を自動生成
'
Sub main()
   
    Dim bookname As String
    bookname = "CUCMメンテナンスシステム_テーブル定義.xlsx"
   
    Dim wb As Workbook
    Dim ws As Worksheet
 
    'Excel文書ファイルをブックオブジェクトとして設定
    Set wb = Workbooks(bookname)
    For Each ws In wb.Worksheets
        'テーブル毎の処理（DDL文の生成）
        sheet_proc ws
    Next
     
    MsgBox "DDL作成完了"
 
End Sub


' テーブルレイアウト生成（シート毎の処理）
'
'   引数はテーブル仕様書のシートオブジェクト
'
Sub sheet_proc(ws As Worksheet)
 
    'DDLファイル生成用
    'Dim f As Integer            ' ファイル番号
    Dim adoSt As Object
    Dim fname As String         ' ファイル名
    Dim tblByte() As Byte       ' Byteテーブル(一時変換用)
     
    'Excelシート読み込み（テーブル情報）
    Dim tname As String         ' テーブル名
    Dim tid As String           ' テーブルID
    Dim tnote As String         ' 要件
         
    'Excelシート読み込み（カラム情報）
    Dim itemno As String        'No
    Dim itempk As String        'プライマリーキー
    Dim itemname As String      '日本語名
    Dim itemid As String        'カラム名
    Dim itemtype As String      'データ型
    Dim itemleng As String      '桁数
    Dim itemnull As String      'NULL
    Dim itemunq As String       'UNIQUE
    Dim itemval As String       '初期値
    Dim itemcycl As String      'サイクル（シーケンス）
     
    Dim pktbl(100) As String    'プライマリーキー保存エリア
    Dim pkcount As Integer      'プライマリーキー保存数
     
    Dim linectr As Integer
    Dim workstr As String       'SQL編集用領域
     
    pkcount = 0                 ' プライマリーキー保存数のクリア
    linectr = 9                 ' カラム開始行のセット（この行から処理をする）
             
    '処理中のシートからテーブル情報を読み取る
    tname = ws.Cells(5, 6)
    tid = ws.Cells(6, 6)
    tnote = ws.Cells(3, 4)
     
    Set adoSt = CreateObject("ADODB.Stream")
    
    'ファイル名の設定（テーブル名付与）
    fname = ActiveWorkbook.Path & "\CreateTable_" & tid & ".sql"
     
 With adoSt
    .Charset = "UTF-8"
    .Open
    
    'Create Table文の自動生成
    .WriteText "CREATE TABLE " & tid & " (", 1
    
    'Noの列に値がある間繰り返す
    While ws.Cells(linectr, 1) <> ""
         
        DoEvents
         
        'カラムの情報を読み取る
        itemno = Trim(ws.Cells(linectr, 1))
        itempk = Trim(ws.Cells(linectr, 2))
        itemname = Trim(ws.Cells(linectr, 3))
        itemid = Trim(ws.Cells(linectr, 9))
        itemtype = Trim(ws.Cells(linectr, 18))
        itemleng = Trim(ws.Cells(linectr, 20))
        itemnull = Trim(ws.Cells(linectr, 21))
        itemunq = Trim(ws.Cells(linectr, 22))
        itemval = Trim(ws.Cells(linectr, 23))
        itemcycl = Trim(ws.Cells(linectr, 27))
         
        'Create Table文の自動生成
        workstr = ""
        'カラム名編集
        workstr = workstr & itemid & " "
             
        'プライマリーキー保存number
        If itempk = "PK" Then
            pkcount = pkcount + 1
            pktbl(pkcount) = itemid
        End If
         
        'サイクル編集
        If itemcycl = "〇" Then
            workstr = workstr & " SERIAL"
        Else
            'データ型編集
            Select Case itemtype
                Case "VARCHAR"
                    workstr = workstr & "varchar("
                    workstr = workstr & itemleng & ")"
                Case "TEXT"
                    workstr = workstr & "text"
                Case "JSONB"
                    workstr = workstr & "json"
                Case "INTEGER"
                    workstr = workstr & "integer"
                Case "TIMESTAMP"
                    workstr = workstr & "timestamp"
            End Select
                     
            '初期値編集（記入時のみ）
            If Len(itemval) > 0 Then
                If itemval = "現在日時" Then
                    workstr = workstr & " DEFAULT CURRENT_TIMESTAMP"
                Else
                    workstr = workstr & " DEFAULT " & itemval
                End If
            End If
             
            'NULL編集
            If itemnull = "〇" Then
                workstr = workstr & ""
            Else
                workstr = workstr & " NOT NULL"
            End If
        End If
       
        workstr = workstr & ", "
        .WriteText workstr, 1
             
        linectr = linectr + 1
     
    Wend
     
    'プライマリーキー情報の編集
    Dim i As Integer
    If pkcount > 0 Then
        'Print #f, " PRIMARY KEY (";
        .WriteText " PRIMARY KEY (", 0
        For i = 1 To pkcount
            'Print #f, pktbl(i);
            .WriteText pktbl(i), 0
            If i < pkcount Then
                'Print #f, ",";
                .WriteText ",", 0
            Else
                'Print #f, ")"
                .WriteText ")", 1
            End If
        Next
    End If
     
    'Print #f, ");"
    .WriteText ");", 1
    
    linectr = 9                 ' カラム開始行のセット（この行から処理をする）
    
    'テーブル論理名
    workstr = "COMMENT ON TABLE " & tid & " IS '" & tnote & "';"
    .WriteText workstr, 1
    
    'Noの列に値がある間繰り返す
    While ws.Cells(linectr, 1) <> ""
         
        DoEvents
         
        'カラムの情報を読み取る
        itemname = Trim(ws.Cells(linectr, 3))
        itemid = Trim(ws.Cells(linectr, 9))
         
        'COMMENT文の自動生成
        workstr = ""
        
        'カラム論理名
        workstr = "COMMENT ON COLUMN " & tid & "." & itemid & " IS '" & itemname & "';"

        .WriteText workstr, 1
         
        linectr = linectr + 1
     
    Wend
    .Position = 0                                ' Stream位置を0にする
    .Type = 1                                    ' バイナリモードに変更
    .Position = 3                                ' Stream位置を3にする
    tblByte = .Read                              ' Stream内容をByteテーブルに格納
    .Close                                       ' 一旦閉じる
    .Open                                        ' 再Open
    .Write tblByte                               ' Byteテーブルを書き出す
    .SetEOS
    .SaveToFile fname, 2
    .Close

End With
 
End Sub